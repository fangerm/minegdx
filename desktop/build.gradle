project.ext.mainClassName = "xyz.angm.terra3d.desktop.DesktopLauncherKt"

task run(dependsOn: classes, type: JavaExec) {
    main = project.mainClassName
    classpath = sourceSets.main.runtimeClasspath
    standardInput = System.in
    workingDir = project.assetsDir
    ignoreExitValue = true
}

task debug(dependsOn: classes, type: JavaExec) {
    main = project.mainClassName
    classpath = sourceSets.main.runtimeClasspath
    standardInput = System.in
    workingDir = project.assetsDir
    ignoreExitValue = true
    debug = true
}

task explodeForWeave(type: Sync, dependsOn: classes) {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    into project.weaveDir
    from files(sourceSets.main.output.classesDirs)
    from files(sourceSets.main.output.resourcesDir)
    from {configurations.runtimeClasspath.collect {zipTree(it)}}
    from files(project.assetsDir)
}

weave {
    dependsOn explodeForWeave
    enableArtemisPlugin = true
    enablePooledWeaving = true
    ideFriendlyPacking = true
    optimizeEntitySystems = true
    classesDir = project.weaveDir
}

task dist(type: Jar, dependsOn: weave) {
    from project.weaveDir
    archiveBaseName = "terra3d-client"
    // Exclude Linux ARM targets
    exclude('linux/arm32/**', 'linux/arm64/**')

    manifest {
        attributes 'Main-Class': project.mainClassName
    }
}

dist.dependsOn classes

eclipse.project.name = appName + "-desktop"